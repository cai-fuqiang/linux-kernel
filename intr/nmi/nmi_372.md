# 372 代码

我们直接看代码:
```cpp
/*
 * Runs on exception stack.  Xen PV does not go through this path at all,
 * so we can use real assembly here.
 *
 * Registers:
 *      %r14: Used to save/restore the CR3 of the interrupted context
 *            when PAGE_TABLE_ISOLATION is in use.  Do not clobber.
 */
SYM_CODE_START(nmi)
        UNWIND_HINT_IRET_REGS

        /*
         * We allow breakpoints in NMIs. If a breakpoint occurs, then
         * the iretq it performs will take us out of NMI context.
         * This means that we can have nested NMIs where the next
         * NMI is using the top of the stack of the previous NMI. We
         * can't let it execute because the nested NMI will corrupt the
         * stack of the previous NMI. NMI handlers are not re-entrant
         * anyway.
         *
         * > 我们允许在NMIs中 breakpoints. 如果 breakpoint 发生了, 将会在
         * > NMI context 之外执行 iretq. 这意味着我们可以使用上一个NMI的栈
         * > 顶来嵌套NMIs. 我们不能让他来执行, 因为nested NMI 将会损坏之前
         * > NMI 的stack. NMI处理程序无论如何都不是可重入的。
         *
         * To handle this case we do the following:
         *  Check the a special location on the stack that contains
         *  a variable that is set when NMIs are executing.
         *  The interrupted task's stack is also checked to see if it
         *  is an NMI stack.
         *  > 检查一个stack 中特殊的位置, 该位置包含一个变量, 当NMIs正在
         *  > 执行的时候, 会去设置. 被中断的task的stack也被检查是否是一个
         *  > NMI stack
         *
         *  If the variable is not set and the stack is not the NMI
         *  stack then:
         *  > 如果 variable 没有被设置, 并且stack 不是 NMI stack, 然后:
         *
         *    o Set the special variable on the stack
         *    > 设置 stack上的 special variable
         *    o Copy the interrupt frame into an "outermost" location on the
         *      stack
         *    > copy interrupt frame 到 stack 上的 "outermost" location
         *    o Copy the interrupt frame into an "iret" location on the stack
         *    > Copy interrupt frame 到 stack 上的 "iret" location
         *
         *    o Continue processing the NMI
         *    > 继续处理NMI
         *
         *  If the variable is set or the previous stack is the NMI stack:
         *  > 如果variable 被设置, 或者 previous stack 是一个 NMI stack
         *    o Modify the "iret" location to jump to the repeat_nmi
         *    > 修改 iret 位置, 跳转到 repect_nmi
         *    o return back to the first NMI
         *    > 跳转回 first NMI
         *
         * Now on exit of the first NMI, we first clear the stack variable
         * The NMI stack will tell any nested NMIs at that point that it is
         * nested. Then we pop the stack normally with iret, and if there was
         * a nested NMI that updated the copy interrupt stack frame, a
         * jump will be made to the repeat_nmi code that will handle the second
         * NMI.
         *
         * > 现在first NMI exit, 我们首先clear stack variable, NMI stack 将会
         * 告诉任何 nested NMIs 此时他是nested. 然后我们使用 iret 正常 pop 这个
         * stack , 然后如果有一个嵌套的NMI更新了 copy interrupt stack frame, 将会
         * 制造一个jump到 repeat_nmi code , 然后将会处理第二个NMI.
         *
         * However, espfix prevents us from directly returning to userspace
         * with a single IRET instruction.  Similarly, IRET to user mode
         * can fault.  We therefore handle NMIs from user space like
         * other IST entries.
         *
         * 然而, espfix 阻止哦我们通过一个 IRET 指令, 直接返回用户态. 简单来说,
         * IRET 到 user mode 将会造成 fault. 我们因此像其他IST 条目一样处理来自用户
         * 空间的NMI.
         */

        ASM_CLAC

        /* Use %rdx as our temp variable throughout */
        //rdx 是一个临时寄存器
        pushq   %rdx

        /*走到这里的堆栈为:
         * | original SS                                             |
         * | original Return RSP                                     |
         * | original RFLAGS                                         |
         * | original CS                                             |
         * | original RIP                                            |
         * +---------------------------------------------------------+
         * | temp storage for rdx                                    |===>RSP
         *
         * 这里 8(%rsp) 是 original RIP address
         * 而为什么又使用 CS-RIP做偏移呢, 而不是 16(%rsp), 因为32位和64位
         * IP 的宽度不同. CS-RIP 就是 IP 的实际宽度
         *
         * 对于用户态来说, CS 为0x33
         * 对于内核态来说, CS 为0x10
         *
         * 这里我们先不关注用户态, 直接来看内核态.
         */
        testb   $3, CS-RIP+8(%rsp)
        jz      .Lnmi_from_kernel
```

## Lnmi_from_kernel

### Lnmi_from_kernel PART 1
```cpp
.Lnmi_from_kernel:
        /*
         * Here's what our stack frame will look like:
         * +---------------------------------------------------------+
         * | original SS                                             |
         * | original Return RSP                                     |
         * | original RFLAGS                                         |
         * | original CS                                             |
         * | original RIP                                            |
         * +---------------------------------------------------------+
         * | temp storage for rdx                                    |
         * +---------------------------------------------------------+
         * | "NMI executing" variable                                |
         * +---------------------------------------------------------+
         * | iret SS          } Copied from "outermost" frame        |
         * | iret Return RSP  } on each loop iteration; overwritten  |
         * | iret RFLAGS      } by a nested NMI to force another     |
         * | iret CS          } iteration if needed.                 |
         * | iret RIP         }                                      |
         * +---------------------------------------------------------+
         * | outermost SS          } initialized in first_nmi;       |
         * | outermost Return RSP  } will not be changed before      |
         * | outermost RFLAGS      } NMI processing is done.         |
         * | outermost CS          } Copied to "iret" frame on each  |
         * | outermost RIP         } iteration.                      |
         * +---------------------------------------------------------+
         * | pt_regs                                                 |
         * +---------------------------------------------------------+
         *
         * The "original" frame is used by hardware.  Before re-enabling
         * NMIs, we need to be done with it, and we need to leave enough
         * space for the asm code here.
         *
         * Hardware 使用的 "original" frame, 在 re-enabling NMIs之前, 
         * 我们需要处理完它, 并且需要为 asm 代码留出足够的空间.
         *
         * We return by executing IRET while RSP points to the "iret" frame.
         * That will either return for real or it will loop back into NMI
         * processing.
         *
         * 当 RSP指向 "iret" frame 时, 我们执行 IRET指令返回. 这里可能真正返回
         * 或者 loop back到 NMI processing中
         *
         * The "outermost" frame is copied to the "iret" frame on each
         * iteration of the loop, so each iteration starts with the "iret"
         * frame pointing to the final return target.
         *
         * iteration: [ˌɪtəˈreɪʃn] 反复迭代 
         *
         * "outermost" frame 将会 copied 到 "iret" frame 中在每个 loop的 iteration
         * 中, 所以每个 iteration 都会在 "iret" frame开始, 指向 最终的 return target
         */
```

### Lnmi_from_kernel PART 2
```cpp
        /*
         * Determine whether we're a nested NMI.
         *
         * If we interrupted kernel code between repeat_nmi and
         * end_repeat_nmi, then we are a nested NMI.  We must not
         * modify the "iret" frame because it's being written by
         * the outer NMI.  That's okay; the outer NMI handler is
         * about to about to call do_nmi anyway, so we can just
         * resume the outer NMI.
         *
         * 如果我们在 repeat_nmi和 end_repeat_nmi kernel code之间interrupt ,
         * 那将是一个 nested NMI. 我们必须不要修改 "iret" frame 因为
         * 正在被outer NMI 修改. 这是可以的; outer NMI handler 无论如何都将要
         * 调用 do_nmi, 所以我们仅仅 resume outer NMI 即可
         */
        /*
         * repeat_nmi和end_repeat_nmi是symbol, 我们下面会讲到
         * 前面说道8(%rsp)是 interrupt stack的 rip
         */
        movq    $repeat_nmi, %rdx
        cmpq    8(%rsp), %rdx           // repeat_nmi < rip
        ja      1f
        movq    $end_repeat_nmi, %rdx   // end_repeat_nmi >= rip
        cmpq    8(%rsp), %rdx
        ja      nested_nmi_out
1:
```
而 rip < repeat_nmi, rip > end_repeat_nmi, 都会走到`1`这个位置

### Lnmi_from_kernel PART3
```cpp
1:

        /*
         * Now check "NMI executing".  If it's set, then we're nested.
         * This will not detect if we interrupted an outer NMI just
         * before IRET.
         *
         * 现在检查 "NMI executing" 部分, 如果被设置了, 说明我们已经
         * nested. 这不会检测我们是否在 IRET 之前, interrupt了 outer NMI.
         *
         * > 如果设置了 "NMI executing" 直接 jump到 nested_nmi
         */
        cmpl    $1, -8(%rsp)
        je      nested_nmi

        /*
         * Now test if the previous stack was an NMI stack.  This covers
         * the case where we interrupt an outer NMI after it clears
         * "NMI executing" but before IRET.  We need to be careful, though:
         * there is one case in which RSP could point to the NMI stack
         * despite there being no NMI active: naughty userspace controls
         * RSP at the very beginning of the SYSCALL targets.  We can
         * pull a fast one on naughty userspace, though: we program
         * SYSCALL to mask DF, so userspace cannot cause DF to be set
         * if it controls the kernel's RSP.  We set DF before we clear
         * "NMI executing".
         *
         * 现在 检测 之前的堆栈是否是NMI stack. 
         */
        lea     6*8(%rsp), %rdx
        /* Compare the NMI stack (rdx) with the stack we came from (4*8(%rsp)) */
        cmpq    %rdx, 4*8(%rsp)
        /* If the stack pointer is above the NMI stack, this is a normal NMI */
        ja      first_nmi

        subq    $EXCEPTION_STKSZ, %rdx
        cmpq    %rdx, 4*8(%rsp)
        /* If it is below the NMI stack, it is a normal NMI */
        jb      first_nmi

        /* Ah, it is within the NMI stack. */

        testb   $(X86_EFLAGS_DF >> 8), (3*8 + 1)(%rsp)
        jz      first_nmi       /* RSP was user controlled. */

        /* This is a nested NMI. */
```


# nested_nmi
```cpp
nested_nmi:
        /*
         * Modify the "iret" frame to point to repeat_nmi, forcing another
         * iteration of NMI handling.
         */
        subq    $8, %rsp
        leaq    -10*8(%rsp), %rdx
        pushq   $__KERNEL_DS
        pushq   %rdx
        pushfq
        pushq   $__KERNEL_CS
        pushq   $repeat_nmi

        /* Put stack back */
        addq    $(6*8), %rsp

nested_nmi_out:
        popq    %rdx

        /* We are returning to kernel mode, so this cannot result in a fault. */
        iretq
```

# first nmi
```
first_nmi:
        /* Restore rdx. */
        movq    (%rsp), %rdx

        /* Make room for "NMI executing". */
        pushq   $0

        /* Leave room for the "iret" frame */
        subq    $(5*8), %rsp

        /* Copy the "original" frame to the "outermost" frame */
        .rept 5
        pushq   11*8(%rsp)
        .endr
        UNWIND_HINT_IRET_REGS

        /* Everything up to here is safe from nested NMIs */

#ifdef CONFIG_DEBUG_ENTRY
        /*
         * For ease of testing, unmask NMIs right away.  Disabled by
         * default because IRET is very expensive.
         */
        pushq   $0              /* SS */
        pushq   %rsp            /* RSP (minus 8 because of the previous push) */
        addq    $8, (%rsp)      /* Fix up RSP */
        pushfq                  /* RFLAGS */
        pushq   $__KERNEL_CS    /* CS */
        pushq   $1f             /* RIP */
        iretq                   /* continues at repeat_nmi below */
        UNWIND_HINT_IRET_REGS
1:
#endif
```

# repeat nmi
```
nmi:
        /*
         * If there was a nested NMI, the first NMI's iret will return
         * here. But NMIs are still enabled and we can take another
         * nested NMI. The nested NMI checks the interrupted RIP to see
         * if it is between repeat_nmi and end_repeat_nmi, and if so
         * it will just return, as we are about to repeat an NMI anyway.
         * This makes it safe to copy to the stack frame that a nested
         * NMI will update.
         *
         * RSP is pointing to "outermost RIP".  gsbase is unknown, but, if
         * we're repeating an NMI, gsbase has the same value that it had on
         * the first iteration.  paranoid_entry will load the kernel
         * gsbase if needed before we call do_nmi.  "NMI executing"
         * is zero.
         */
        movq    $1, 10*8(%rsp)          /* Set "NMI executing". */

        /*
         * Copy the "outermost" frame to the "iret" frame.  NMIs that nest
         * here must not modify the "iret" frame while we're writing to
         * it or it will end up containing garbage.
         */
        addq    $(10*8), %rsp
        .rept 5
        pushq   -6*8(%rsp)
        .endr
        subq    $(5*8), %rsp
end_repeat_nmi:

        /*
         * Everything below this point can be preempted by a nested NMI.
         * If this happens, then the inner NMI will change the "iret"
         * frame to point back to repeat_nmi.
         */
        pushq   $-1                             /* ORIG_RAX: no syscall to restart */

        /*
         * Use paranoid_entry to handle SWAPGS, but no need to use paranoid_exit
         * as we should not be calling schedule in NMI context.
         * Even with normal interrupts enabled. An NMI should not be
         * setting NEED_RESCHED or anything that normal interrupts and
         * exceptions might do.
         */
        call    paranoid_entry
        UNWIND_HINT_REGS

        /* paranoidentry do_nmi, 0; without TRACE_IRQS_OFF */
        movq    %rsp, %rdi
        movq    $-1, %rsi
        call    do_nmi
```
